//file:noinspection GradlePackageVersionRange
plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'maven-publish'
    id 'io.github.juuxel.loom-quiltflower' version '1.8.0'
}

version = "${project.mod_version}+${project.minecraft_base_version}"
group = project.maven_group

loom {
    accessWidenerPath = project.file("src/main/resources/affinity.accesswidener");

    runs {
        datagen {
            server()
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated")}"

            ideConfigGenerated = true
            runDir "build/datagen"
        }
    }
}

sourceSets {
    main {
        resources {
            srcDirs += ["src/generated"]
        }
    }
}

repositories {
    // modmenu, trinkets
    maven { url "https://maven.terraformersmc.com/" }

    // cca, pal
    maven { url "https://ladysnake.jfrog.io/artifactory/mods" }

    // owo, the one probe
    maven { url "https://maven.wispforest.io" }

    // rei
    maven { url "https://maven.shedaniel.me/" }

    // terrablender
    maven { url "https://maven.minecraftforge.net/" }

    // canvas
    maven { url "https://maven.vram.io/" }

    // emoji-type and lazydfu
    maven {
        url "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }

    // gadget
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }

    mavenLocal()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // actual dependencies

    modLocalRuntime "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${project.rei_version}"

    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca_version}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-chunk:${project.cca_version}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-world:${project.cca_version}"

    modImplementation "dev.emi:trinkets:${project.trinkets_version}"

    modImplementation "io.github.ladysnake:PlayerAbilityLib:${project.pal_version}"
    include "io.github.ladysnake:PlayerAbilityLib:${project.pal_version}"

    modImplementation "io.wispforest:owo-lib:${project.owo_version}"
    include "io.wispforest:owo-sentinel:${project.owo_version}"

    modImplementation("io.wispforest:owo-whats-this:${project.owo_whats_this_version}") {
        exclude group: "maven.modrinth"
    }

    modImplementation "com.github.glitchfiend:TerraBlender-fabric:${project.terrablender_version}"

    modImplementation "io.wispforest:worldmesher:${project.worldmesher_version}"

    // sky block compatibility
    modCompileOnly "maven.modrinth:sodium:${project.sodium_version}"
    modCompileOnly "maven.modrinth:iris:${project.iris_version}"

    // dev utils

    modLocalRuntime "maven.modrinth:emoji-type:2.0.0+1.19"
    modLocalRuntime "maven.modrinth:lazydfu:0.1.3"

//    modLocalRuntime "io.vram:canvas-fabric-mc119:${project.canvas_version}"
//    modLocalRuntime "io.wispforest:gadget:${project.gadget_version}"
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {}
}
